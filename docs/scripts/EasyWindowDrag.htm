<!DOCTYPE HTML>
<html>
<head>
<title>Easy Window Dragging (requires XP/2k/NT)</title>
<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
<meta http-equiv="X-UA-Compatible" content="IE=edge">
<link href="../static/theme.css" rel="stylesheet" type="text/css" />
<script src="../static/content.js" type="text/javascript"></script>
</head>
<body>

<h1>Easy Window Dragging (requires XP/2k/NT)</h1>

<p>Normally, a window can only be dragged by clicking on its title bar.
 This script extends that so that any point inside a window can be dragged.
 To activate this mode, hold down CapsLock or the middle mouse button while
 clicking, then drag the window to a new position.
</p>
<p><a href="EasyWindowDrag.ahk">Download This Script</a> &nbsp;| &nbsp;<a href="index.htm">Other Sample Scripts</a> &nbsp;| &nbsp;<a href="../AutoHotkey.htm">Home</a></p>

<pre class="NoIndent"><em>; Note: You can optionally release CapsLock or the middle mouse button after
; pressing down the mouse button rather than holding it down the whole time.</em>

~MButton &amp; LButton::
CapsLock &amp; LButton::
EWD_MoveWindow()
{
    CoordMode "Mouse"  <em>; Switch to screen/absolute coordinates.</em>
    MouseGetPos EWD_MouseStartX, EWD_MouseStartY, EWD_MouseWin
    WinGetPos EWD_OriginalPosX, EWD_OriginalPosY,,, "ahk_id " EWD_MouseWin
    if !WinGetMinMax("ahk_id " EWD_MouseWin)  <em>; Only if the window isn't maximized </em>
        SetTimer "EWD_WatchMouse", 10 <em>; Track the mouse as the user drags it.</em>

    EWD_WatchMouse()
    {
        if !GetKeyState("LButton", "P")  <em>; Button has been released, so drag is complete.</em>
        {
            SetTimer , "Off"
            return
        }
        if GetKeyState("Escape", "P")  <em>; Escape has been pressed, so drag is cancelled.</em>
        {
            SetTimer , "Off"
            WinMove EWD_OriginalPosX, EWD_OriginalPosY,,, "ahk_id " EWD_MouseWin
            return
        }
        <em>; Otherwise, reposition the window to match the change in mouse coordinates
        ; caused by the user having dragged the mouse:</em>
        CoordMode "Mouse"
        MouseGetPos EWD_MouseX, EWD_MouseY
        WinGetPos EWD_WinX, EWD_WinY,,, "ahk_id " EWD_MouseWin
        SetWinDelay -1   <em>; Makes the below move faster/smoother.</em>
        WinMove EWD_WinX + EWD_MouseX - EWD_MouseStartX, EWD_WinY + EWD_MouseY - EWD_MouseStartY,,, "ahk_id " EWD_MouseWin
        EWD_MouseStartX := EWD_MouseX  <em>; Update for the next timer-call to this subroutine.</em>
        EWD_MouseStartY := EWD_MouseY
    }
}
</pre>
</body>
</html>
