<!DOCTYPE HTML>
<html>
<head>
<title>Seek</title>
<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
<meta http-equiv="X-UA-Compatible" content="IE=edge">
<link href="../static/theme.css" rel="stylesheet" type="text/css" />
<script src="../static/content.js" type="text/javascript"></script>
</head>
<body>

<h1>Seek <span class="headnote">Based on the v1 script by Phi</span></h1>

<p>Navigating the Start Menu can be a hassle, especially
 if you have installed many programs over time. 'Seek'
 lets you specify a case-insensitive key word/phrase
 that it will use to filter only the matching programs
 and directories from the Start Menu, so that you can
 easily open your target program from a handful of
 matched entries. This eliminates the drudgery of
 searching and traversing the Start Menu.
</p>
<p><a href="Seek_(SearchTheStartMenu).ahk">Download This Script</a> &nbsp;| &nbsp;<a href="index.htm">Other Sample Scripts</a> &nbsp;| &nbsp;<a href="../AutoHotkey.htm">Home</a></p>

<pre class="NoIndent"><em>/*
Options:

-cache Use the cached directory-listing if available (this is the default mode
       when no option is specified)
-scan  Force a directory scan to retrieve the latest directory listing
-scex  Scan & exit (this is useful for scheduling the potentially
       time-consuming directory-scanning as a background job)
-help  Show this help

Important notes:

Check AutoHotkey's Tutorial how to run this script, or to compile it if
necessary.

The only file 'Seek' creates is placed in your TMP directory:

  a. _Seek.ini  (cache file for last query string and directory listing)

When you run 'Seek' for the first time, it'll scan your Start Menu,
and save the directory listing into a cache file.

The following directories are included in the scanning:
- A_StartMenu
- A_StartMenuCommon

By default, subsequent runs will read from the cache file so as to reduce the
loading time. For more info on options, run 'Seek.exe -help'. If you think your
Start Menu doesn't contain too many programs, you can choose not to use the
cache and instruct 'Seek' to always do a directory scan (via option -scan).
That way, you will always get the latest listing.

When you run 'Seek', a window will appear, waiting for you to enter a
key word/phrase. After you have entered a query string, a list of
matching records will be displayed. Next, you need to highlight an entry and
press ENTER or click on the 'Open' button to run the selected program or open
the selected directory.
*/</em>

<em>/*
Specify which program to use when opening a directory. If the program cannot
be found or is not specified (i.e. variable is unassigned or assigned
a null value), the default Explorer will be used.
*/</em>
config := {dirExplorer: "E:\utl\xplorer2_lite\xplorer2.exe"}

<em>/*
User's customised list of additional directories to be included in the
scanning. The full path must not be enclosed by quotes or double-quotes.
If this file is missing, only the default directories will be scanned.
*/</em>
config.SeekMyDir := A_ScriptDir "\Seek.dir"

<em>/*
Specify the filename and directory location to save the cached directory/program
listing and the cached key word/phrase of the last search.
There is no need to change this unless you want to.
*/</em>
config.saveFile := A_Temp "\_Seek.ini"

<em>/*
Track search string (True/False)
If true, the last-used query string will be re-used as the default query
string the next time you run Seek. If false, the last-used query string will
not be tracked and there will not be a default query string value the
next time you run Seek.
*/</em>
config.TrackKeyPhrase := True

<em>/*
Specify what should be included in scan.
 F: Files are included.
 D: Directories are included.
*/</em>
config.ScanMode := "FD"

<em>; Init:
; #NoTrayIcon

; Define the script title:</em>
config.ScriptTitle := "Seek - Search the Start Menu"

<em>; Display the help instructions:</em>
if A_Args[1] ~= "^(--help|-help|/h|-h|/\?|-\?)$"
{
    MsgBox("
    (
        Navigating the Start Menu can be a hassle, especially if you have installed
        many programs over time. 'Seek' lets you specify a case-insensitive key
        word/phrase that it will use to filter only the matching programs and
        directories from the Start Menu, so that you can easily open your target
        program from a handful of matched entries. This eliminates the drudgery of
        searching and traversing the Start Menu.

        Options:
          -cache  Use the cached directory-listing if available (this is the default mode
            when no option is specified)
          -scan  Force a directory scan to retrieve the latest directory listing
          -scex  Scan & exit (this is useful for scheduling the potentially
            time-consuming directory-scanning as a background job)
          -help  Show this help
    )", config.ScriptTitle)
    ExitApp
}

<em>; Check that the mandatory environment variables exist and are valid:</em>
if !FileExist(A_Temp) <em>; Path does not exist.</em>
{
    MsgBox
    (
        "This mandatory environment variable is either not defined or invalid:
        
            TMP = " A_Temp "
            
        Please fix it before running Seek."
    ), config.ScriptTitle
    ExitApp
}

<em>; Scan the Start Menu without Gui:</em>
if A_Args[1] = "-scex"
{
    SaveFileList(config)
    return
}

<em>; Create the GUI window:</em>
G := GuiCreate(, config.ScriptTitle)

<em>; Add the text box for user to enter the query string:</em>
E_Search := G.Add("Edit", "W600")
if config.TrackKeyPhrase
    E_Search.Value := IniRead(config.saveFile, "LastSession", "SearchText")

<em>; Add my fav tagline:</em>
G.Add("Text", "X625 Y10", "What do you seek, my friend?")

<em>; Add the status bar for providing feedback to user:</em>
T_Info := G.Add("Text", "X10 Y31 R1 W764")

<em>; Add the selection listbox for displaying search results:</em>
LB := G.Add("ListBox", "X10 Y53 R28 W764 HScroll Disabled")

<em>; Add these buttons, but disable them for now:</em>
B_1 := G.Add("Button", "Default X10 Y446 Disabled", "Open")
B_2 := G.Add("Button", "X59 Y446 Disabled", "Open Directory")
B_3 := G.Add("Button", "X340 Y446", "Scan Start-Menu")

<em>; Add the Exit button:</em>
B_4 := G.Add("Button", "X743 Y446", "Exit")

<em>; Create function references for event callbacks:</em>
FindMatches := Func("FindMatches").bind(config, LB, B_1, B_2)
OpenTarget := Func("OpenTarget").bind(config, LB)
OpenFolder := Func("OpenFolder").bind(config, LB)
ScanStartMenu := Func("ScanStartMenu").bind(config, E_Search, T_Info, LB, B_1, B_2, B_3)
Gui_Close := Func("Gui_Close").bind(config, E_Search, LB)

<em>; Add control events:</em>
E_Search.OnEvent("Change", FindMatches)
LB.OnEvent("DoubleClick", OpenTarget)
B_1.OnEvent("Click", OpenTarget)
B_2.OnEvent("Click", OpenFolder)
B_3.OnEvent("Click", ScanStartMenu)
B_4.OnEvent("Click", Gui_Close)

<em>; Add window events:</em>
G.OnEvent("Close", Gui_Close)
G.OnEvent("Escape", Gui_Close)

<em>; Pop-up the query window:</em>
G.Show("Center")

<em>; Force re-scanning if -scan is enabled or listing cache file does not exist:</em>
if (A_Args[1] = "-scan" or !FileExist(config.saveFile))
    ScanStartMenu(config, E_Search, T_Info, LB, B_1, B_2, B_3)

<em>; Retrieve an set the matching list:</em>
FindMatches(config, LB, B_1, B_2, E_Search)

<em>; Retrieve the last selection from cache file and select the item:</em>
if config.TrackKeyPhrase
    if (LastSelection := IniRead(config.saveFile, "LastSession", "Selection"))
        LB.Choose(LastSelection)

<em>; Function definitions ---

; Scan the start-menu and store the directory/program listings in a cache file:</em>
ScanStartMenu(config, E_Search, T_Info, LB, B_1, B_2, B_3)
{
    <em>; Inform user that scanning has started:</em>
    T_Info.Value := "Scanning directory listing..."

    <em>; Disable listbox while scanning is in progress:</em>
    LB.Enabled := false
    B_1.Enabled := false
    B_2.Enabled := false
    B_3.Enabled := false

    <em>; Retrieve and save the start menu files:</em>
    SaveFileList(config)
    
    <em>; Inform user that scanning has completed:</em>
    T_Info.Value := "Scan completed."
    
    <em>; Enable listbox:</em>
    LB.Enabled := true
    B_1.Enabled := true
    B_2.Enabled := true
    B_3.Enabled := true
    
    <em>; Filter for search string with the new listing:</em>
    FindMatches(config, LB, B_1, B_2, E_Search)
}

<em>; Retrieve and save the start menu files:</em>
SaveFileList(config)
{
    <em>; Define the directory paths to retrieve:</em>
    LocationArray := [A_StartMenu, A_StartMenuCommon]

    <em>; Include additional user-defined paths for scanning:</em>
    if FileExist(config.SeekMyDir)
        Loop Read, config.SeekMyDir
        {
            if !FileExist(A_LoopReadLine)
                MsgBox
                (
                    "Processing your customised directory list...
                    
                    '" A_LoopReadLine "' does not exist and will be excluded from the scanning.
                    Please update [ " config.SeekMyDir " ]."
                ), config.ScriptTitle, 8192
            else
                LocationArray.Push(A_LoopReadLine)
        }

    <em>; Scan directory listing by recursing each directory to retrieve the contents.
    ; Hidden files are excluded:</em>
    IniDelete(config.saveFile, "LocationList")
    For i, Location in LocationArray
    {
        <em>; Save space by using relative paths:</em>
        IniWrite(Location, config.saveFile, "LocationList", "L" i)
        A_WorkingDir := Location
        Loop Files, "*", config.ScanMode "R"
            if !InStr(FileGetAttrib(A_LoopFilePath), "H") <em>; Exclude hidden file.</em>
                FileList .= "%L" i "%\" A_LoopFilePath "`n"
    }
    IniDelete(config.saveFile, "FileList")
    IniWrite(FileList, config.saveFile, "FileList")
}

<em>; Search and display all matching records in the listbox:</em>
FindMatches(config, LB, B_1, B_2, E_Search)
{
    FileArray := []
    SearchText := E_Search.Value
    <em>; Filter matching records based on user query string:</em>
    if SearchText
    {
        Loop
        {
            Location := IniRead(config.saveFile, "LocationList", "L" A_Index)
            if !Location
                break
            L%A_Index% := Location
        }
        Loop Parse, IniRead(config.saveFile, "FileList"), "`n"
        {
            Line := A_LoopField
            if RegExMatch(Line, "%(L\d+)%", m) <em>; Replace %L_n% with location paths.</em>
                Line := StrReplace(Line, "%" m[1] "%", %m[1]%)
            if SearchText &lt;&gt; E_Search.Value
            {
                <em>; User has changed the search string.
                ; There is no point to continue searching using the old string, so abort.</em>
                return
            }
            else
            {
                <em>; Append matching records into the list:</em>
                SplitPath(Line, Name)
                MatchFound := true
                Loop Parse, SearchText, "`s"
                {
                    if !InStr(Name, A_LoopField)
                    {
                        MatchFound := false
                        break
                    }
                }
                if MatchFound
                    FileArray.Push(Line)
            }
        }
    }

    <em>; Refresh list with search results:</em>
    LB.Delete(), LB.Add(FileArray)

    if !FileArray.Length()
    {
        <em>; No matching record is found. Disable listbox:</em>
        LB.Enabled := false
        B_1.Enabled := false
        B_2.Enabled := false
    }
    else
    {
        <em>; Matching records are found. Enable listbox:</em>
        LB.Enabled := true
        B_1.Enabled := true
        B_2.Enabled := true
        <em>; Select the first record if no other record has been selected:</em>
        if LB.Text = ""
            LB.Choose(1, 1)
    }
}

<em>; User clicked on 'Open' button or pressed ENTER:</em>
OpenTarget(config, LB)
{
    <em>; Selected record does not exist (file or directory not found):</em>
    if !FileExist(LB.Text)
    {
        MsgBox
        (
            LB.Text " does not exist.
            
            This means that the directory cache is outdated. You may click on
            the 'Scan Start-Menu' button below to update the directory cache with your
            latest directory listing now."
        ), config.ScriptTitle, 8192
        return
    }

    <em>; Check whether the selected record is a file or directory:</em>
    fileAttrib := FileGetAttrib(LB.Text)
    if InStr(fileAttrib, "D") <em>; is directory</em>
        OpenFolder(config, LB)
    else if fileAttrib <em>; is file</em>
        Run(LB.Text)
    else
    {
        MsgBox
        (
            LB.Text " is neither a DIRECTORY or a FILE.
            
            This shouldn't happen. Seek cannot proceed. Quitting..."
        )
    }
    WinClose
}

<em>; User clicked on 'Open Directory' button:</em>
OpenFolder(config, LB)
{
    Path := LB.Text
    <em>; If user selected a file-record instead of a directory-record, extract the
    ; directory path (I'm using DriveGetStatus instead of FileGetAttrib to allow the
    ; scenario whereby LB.Text is invalid but the directory path of LB.Text is valid):</em>
    if DriveGetStatus(Path) &lt;&gt; "Ready" <em>; not a directory</em>
    {
        SplitPath(Path,, Dir)
        Path := Dir
    }

    <em>; Check whether directory exists:</em>
    if !FileExist(Path)
    {
        MsgBox
        (
            Path " does not exist.
            
            This means that the directory cache is outdated. You may click on
            the 'Scan Start-Menu' button below to update the directory cache with your
            latest directory listing now."
        ), config.ScriptTitle, 8192
        return
    }

    <em>; Open the directory:</em>
    if FileExist(config.dirExplorer)
        Run(Format('"{1}" "{2}"', config.dirExplorer, Path)) <em>; Open with custom file explorer.</em>
    else
        Run(Path) <em>; Open with default windows file explorer.</em>
}


Gui_Close(config, E_Search, LB)
{
    <em>; Save the key word/phrase for next run:</em>
    if config.TrackKeyPhrase
    {
        IniWrite(E_Search.Value, config.saveFile, "LastSession", "SearchText")
        IniWrite(LB.Text, config.saveFile, "LastSession", "Selection")
    }
    ExitApp
}
</pre>
</body>
</html>
