<!DOCTYPE HTML>
<html>
<head>
<title>Easy Window Dragging -- KDE style (requires XP/2k/NT) -- by Jonny</title>
<meta http-equiv="Content-Type" content="text/html; charset=iso-8859-1">
<meta http-equiv="X-UA-Compatible" content="IE=edge">
<link href="../static/theme.css" rel="stylesheet" type="text/css" />
<script src="../static/content.js" type="text/javascript"></script>
</head>
<body>

<h1>Easy Window Dragging -- KDE style (requires XP/2k/NT) -- by Jonny</h1>

<p>This script makes it much easier to move or resize a window: 1) Hold down
 the ALT key and LEFT-click anywhere inside a window to drag it to a new
 location; 2) Hold down ALT and RIGHT-click-drag anywhere inside a window
 to easily resize it; 3) Press ALT twice, but before releasing it the second
 time, left-click to minimize the window under the mouse cursor, right-click
 to maximize it, or middle-click to close it.
</p>
<p><a href="EasyWindowDrag_(KDE).ahk">Download This Script</a> &nbsp;| &nbsp;<a href="index.htm">Other Sample Scripts</a> &nbsp;| &nbsp;<a href="../AutoHotkey.htm">Home</a></p>

<pre class="NoIndent"><em>; This script was inspired by and built on many like it
; in the forum. Thanks go out to ck, thinkstorm, Chris,
; and aurelian for a job well done.</em>

<em>; Change history:
; November 07, 2006: Optimized resizing code in !RButton, courtesy of bluedawn.
; February 05, 2006: Fixed double-alt (the ~Alt hotkey) to work with latest versions of AHK.</em>

<em>; The Double-Alt modifier is activated by pressing
; Alt twice, much like a double-click. Hold the second
; press down until you click.
;
; The shortcuts:
;  Alt + Left Button  : Drag to move a window.
;  Alt + Right Button : Drag to resize a window.
;  Double-Alt + Left Button   : Minimize a window.
;  Double-Alt + Right Button  : Maximize/Restore a window.
;  Double-Alt + Middle Button : Close a window.
;
; You can optionally release Alt after the first
; click rather than holding it down the whole time.</em>

If (A_AhkVersion &lt; &quot;1.0.39.00&quot;)
{
    MsgBox,20,,This script may not work properly with your version of AutoHotkey. Continue?
    IfMsgBox,No
    ExitApp
}


<em>; This is the setting that runs smoothest on my
; system. Depending on your video card and cpu
; power, you may want to raise or lower this value.</em>
SetWinDelay,2

CoordMode,Mouse
return

!LButton::
If DoubleAlt
{
    MouseGetPos,,,KDE_id
    <em>; This message is mostly equivalent to WinMinimize,</em>
    <em>; but it avoids a bug with PSPad.</em>
    PostMessage,0x112,0xf020,,,ahk_id %KDE_id%
    DoubleAlt := false
    return
}
<em>; Get the initial mouse position and window id, and
; abort if the window is maximized.</em>
MouseGetPos,KDE_X1,KDE_Y1,KDE_id
WinGet,KDE_Win,MinMax,ahk_id %KDE_id%
If KDE_Win
    return
<em>; Get the initial window position.</em>
WinGetPos,KDE_WinX1,KDE_WinY1,,,ahk_id %KDE_id%
Loop
{
    GetKeyState,KDE_Button,LButton,P <em>; Break if button has been released.</em>
    If KDE_Button = U
        break
    MouseGetPos,KDE_X2,KDE_Y2 <em>; Get the current mouse position.</em>
    KDE_X2 -= KDE_X1 <em>; Obtain an offset from the initial mouse position.</em>
    KDE_Y2 -= KDE_Y1
    KDE_WinX2 := (KDE_WinX1 + KDE_X2) <em>; Apply this offset to the window position.</em>
    KDE_WinY2 := (KDE_WinY1 + KDE_Y2)
    WinMove,ahk_id %KDE_id%,,%KDE_WinX2%,%KDE_WinY2% <em>; Move the window to the new position.</em>
}
return

!RButton::
If DoubleAlt
{
    MouseGetPos,,,KDE_id
    <em>; Toggle between maximized and restored state.</em>
    WinGet,KDE_Win,MinMax,ahk_id %KDE_id%
    If KDE_Win
        WinRestore,ahk_id %KDE_id%
    Else
        WinMaximize,ahk_id %KDE_id%
    DoubleAlt := false
    return
}
<em>; Get the initial mouse position and window id, and
; abort if the window is maximized.</em>
MouseGetPos,KDE_X1,KDE_Y1,KDE_id
WinGet,KDE_Win,MinMax,ahk_id %KDE_id%
If KDE_Win
    return
<em>; Get the initial window position and size.</em>
WinGetPos,KDE_WinX1,KDE_WinY1,KDE_WinW,KDE_WinH,ahk_id %KDE_id%
<em>; Define the window region the mouse is currently in.
; The four regions are Up and Left, Up and Right, Down and Left, Down and Right.</em>
If (KDE_X1 &lt; KDE_WinX1 + KDE_WinW / 2)
   KDE_WinLeft := 1
Else
   KDE_WinLeft := -1
If (KDE_Y1 &lt; KDE_WinY1 + KDE_WinH / 2)
   KDE_WinUp := 1
Else
   KDE_WinUp := -1
Loop
{
    GetKeyState,KDE_Button,RButton,P <em>; Break if button has been released.</em>
    If KDE_Button = U
        break
    MouseGetPos,KDE_X2,KDE_Y2 <em>; Get the current mouse position.</em>
    <em>; Get the current window position and size.</em>
    WinGetPos,KDE_WinX1,KDE_WinY1,KDE_WinW,KDE_WinH,ahk_id %KDE_id%
    KDE_X2 -= KDE_X1 <em>; Obtain an offset from the initial mouse position.</em>
    KDE_Y2 -= KDE_Y1
    <em>; Then, act according to the defined region.</em>
    WinMove,ahk_id %KDE_id%,, KDE_WinX1 + (KDE_WinLeft+1)/2*KDE_X2  <em>; X of resized window</em>
                            , KDE_WinY1 +   (KDE_WinUp+1)/2*KDE_Y2  <em>; Y of resized window</em>
                            , KDE_WinW  -     KDE_WinLeft  *KDE_X2  <em>; W of resized window</em>
                            , KDE_WinH  -       KDE_WinUp  *KDE_Y2  <em>; H of resized window</em>
    KDE_X1 := (KDE_X2 + KDE_X1) <em>; Reset the initial position for the next iteration.</em>
    KDE_Y1 := (KDE_Y2 + KDE_Y1)
}
return

<em>; &quot;Alt + MButton&quot; may be simpler, but I
; like an extra measure of security for
; an operation like this.</em>
!MButton::
If DoubleAlt
{
    MouseGetPos,,,KDE_id
    WinClose,ahk_id %KDE_id%
    DoubleAlt := false
    return
}
return

<em>; This detects &quot;double-clicks&quot; of the alt key.</em>
~Alt::
DoubleAlt := A_PriorHotKey = &quot;~Alt&quot; AND A_TimeSincePriorHotkey &lt; 400
Sleep 0
KeyWait Alt  <em>; This prevents the keyboard's auto-repeat feature from interfering.</em>
return
</pre>
</body>
</html>
